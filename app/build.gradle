buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

task printProps {
    doLast {
        println GOOGLE_MAPS_API_KEY
    }
}

//https://developer.android.com/studio/build/build-variants.html#signing
//https://developer.android.com/studio/build/manifest-build-variables.html
android {
    signingConfigs {
        release {
            keyPassword KEY_PASSWORD
            storeFile file('C:/Users/jane/.keystore/android-nega.jks')
            storePassword STORE_PASSWORD
            keyAlias "nega-keystore"
        }
        debug {
            keyPassword KEY_PASSWORD
            storeFile file('C:/Users/jane/.keystore/android-nega-debug.jks')
            storePassword STORE_PASSWORD
            keyAlias "nega-keystore-debug"
        }
    }
    compileSdkVersion 26
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.janeullah.apps.healthinspectionviewer"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [google_map_key: GOOGLE_MAPS_API_KEY]
        }
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.debug
            manifestPlaceholders = [google_map_key: GOOGLE_MAPS_API_KEY]
        }

        //http://stackoverflow.com/questions/35722904/saving-the-api-key-in-gradle-properties
        //http://stackoverflow.com/questions/33134031/is-there-a-safe-way-to-manage-api-keys
        buildTypes.each {
            it.buildConfigField 'String', 'YELP_CLIENT_ID', YELP_CLIENT_ID
            it.buildConfigField 'String', 'YELP_CLIENT_SECRET', YELP_CLIENT_SECRET
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:25.3.1'
    resolutionStrategy.force 'com.android.support:appcompat-v7:25.3.1'
    resolutionStrategy.force 'com.squareup.okhttp3:okhttp:3.7.0'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }
    compile "com.android.support:appcompat-v7:$compile_sdk_version"
    compile "com.android.support:recyclerview-v7:$compile_sdk_version"
    compile "com.android.support:cardview-v7:$compile_sdk_version"
    compile "com.android.support:design:$compile_sdk_version"
    compile "com.android.support:support-v4:$compile_sdk_version"
    compile "com.google.firebase:firebase-messaging:$firebase_version"
    compile "com.google.firebase:firebase-database:$firebase_version"
    compile "com.google.firebase:firebase-crash:$firebase_version"
    compile('com.squareup.retrofit2:retrofit:2.2.0') {
        exclude module: 'okhttp'
    }
    compile "com.google.android.gms:play-services-maps:$google_maps_services_version"
    compile 'com.google.maps:google-maps-services:0.1.20'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.firebaseui:firebase-ui-database:1.2.0'
    compile 'org.parceler:parceler-api:1.1.6'
    compile 'com.jakewharton:butterknife:8.5.1'
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    compile 'org.apache.commons:commons-text:1.0'
    compile 'com.yelp.clientlib:yelp-android:3.0.0'
    compile 'com.google.inject:guice:4.1.0'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    annotationProcessor 'org.parceler:parceler:1.1.6'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
}

apply plugin: 'com.google.gms.google-services'
